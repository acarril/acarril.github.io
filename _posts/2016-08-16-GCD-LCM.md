---
layout: post
title:  "Computing greatest common denominators and least common multiples in Stata"
categories: stata
---

Admittedly, it isn't very often that I need to compute the **greatest common denominator** (GCD) or the **least common multiple** (LCM) in Stata. However, there is no official function that computes them nor any mention on the internet (AFAIK), so here I post small programs that do so.

The need for both calculations arised while thinking about the misfits problem, which I discuss in my [working paper](https://www.researchgate.net/publication/292091060_Dealing_with_misfits_in_random_treatment_assignment). In my related command, [randtreat](https://ideas.repec.org/c/boc/bocode/s458106.html), I use the following three programs to compute both the GCD and the LCM of several numbers.

The "trickiest" part was to compute the LCM for an arbitrarily long list of integers, which is done by `lcmm` (see below):

Ok, so here's the code. There's 4 programs defined, but the first two (namely `gcd` and `lcm`) are auxiliary. **The programs to use are `gcdm` and `lcmm`** (last `m` for "multiple"), which can compute their respective operations **accepting an abritrarily long list of integers.**

```stata
* Greatest Common Denominator (GCD) of 2 integers
program define gcd, rclass
    if "`2'" == "" {
        return scalar gcd = `1'
    }
    else {
        while `2' {
            local temp2 = `2'
            local 2 = mod(`1',`2')
            local 1 = `temp2'
        }
        return scalar gcd = `1'
    }
end

* Least Common Multiple (LCM) of 2 integers
program define lcm, rclass
    if "`2'" == "" {
        return scalar lcm = `1'
    }
    else {
        gcd `1' `2'
        return scalar lcm = `1' * `2' / r(gcd)
    }
end

* LCM of arbitrarily long list of integers
program define lcmm, rclass
    clear results
    foreach i of local 0 {
        lcm `i' `r(lcm)'
    }
    return scalar lcm = r(lcm)
	di "`r(lcm)'"
end

* GCD of arbitrarily long list of integers
program define gcdm, rclass
    clear results
    foreach i of local 0 {
        gcd `i' `r(gcd)'
    }
    return scalar gcd = r(gcd)
	di "`r(gcd)'"
end
```

Notice that the first two auxiliary programs **`gcd` and `lcm` accept a maximum of two inputs**. The computations results aren't displayed, but you can check the returned scalars. For example,

```stata
. gcd 6 9

. di r(gcd)
3
```

The same example for the LCM of 6 and 9 would be

```stata
. lcm 6 9

. di r(lcm)
18
```

Programs **`gcdm` and `lcmm` can compute their respective operations for multiple inputs** exploiting the fact that both operations are associative, that is,

$$\gcd(a,b,c) = \gcd(a,\gcd(b,c))$$

$$\DeclareMathOperator{\lcm}{lcm} \lcm(a,b,c) = \lcm(a,\gcd(b,c))$$

So in Stata you can use

```stata
. gcdm 60 24 36
12

. lcmm 6 9 15
90
```
